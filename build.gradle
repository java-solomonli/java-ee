plugins {
    id 'java'
    id 'war'
}

group 'com.javasl'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly 'jakarta.platform:jakarta.jakartaee-web-api:11.0.0'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.4'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.6'

    implementation 'org.eclipse:yasson:3.0.4'
    implementation 'jakarta.json.bind:jakarta.json.bind-api:3.0.1'
    implementation 'jakarta.json:jakarta.json-api:2.1.3'

    implementation 'org.hibernate.orm:hibernate-core:7.1.4.Final'
    implementation 'com.h2database:h2:2.4.240'

    // wildfly compatibility
    providedCompile 'org.slf4j:slf4j-api:1.7.36'
}

testing {
    suites {
        // ---- Default suite: standard Weld-embedded "unit" tests ----
        test {
            useJUnitJupiter()

            dependencies {
                implementation 'org.jboss.arquillian.junit5:arquillian-junit5-container:1.10.0.Final'
                implementation 'org.jboss.arquillian.container:arquillian-weld-embedded:4.0.0.Final'
                // Jakarta REST API (needed when using @Path, @GET, etc. in embedded)
                implementation 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'
                implementation 'org.junit.jupiter:junit-jupiter-api:${junitVersion}'
                runtimeOnly  'org.junit.jupiter:junit-jupiter-engine:${junitVersion}'
            }
        }

        // ---- Additional suite: remote WildFly integration tests ----
        integrationTest(JvmTestSuite) {
            useJUnitJupiter()

            dependencies {
                implementation project()
                implementation 'org.jboss.arquillian.junit5:arquillian-junit5-container:1.10.0.Final'

                // Jakarta REST API (needed when using @Path, @GET, etc. in embedded)
                implementation 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'
                implementation 'org.jboss.weld.se:weld-se-core:5.1.0.Final'
                implementation 'io.rest-assured:rest-assured:5.5.0'
                runtimeOnly  'org.junit.jupiter:junit-jupiter-engine:${junitVersion}'

                // For RunAsClient
                implementation 'jakarta.json.bind:jakarta.json.bind-api:3.0.1'
                implementation 'org.eclipse:yasson:3.0.4'

                implementation 'org.wildfly.arquillian:wildfly-arquillian-container-managed:5.1.0.Final'
            }

            targets {
                all {
                    testTask.configure {
                        description = "Runs WildFly remote integration tests"
                        group = "verification"
                        useJUnitPlatform()
                        testLogging {
                            events "passed", "skipped", "failed"
                        }
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}
